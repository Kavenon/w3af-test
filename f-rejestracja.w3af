# -----------------------------------------------------------------------------------------------------------
#                                              W3AF AUDIT SCRIPT FOR WEB APPLICATION
# -----------------------------------------------------------------------------------------------------------
#Configure HTTP settings
http-settings
set timeout 30
back

# -------------------------------------
#               MISC
# -------------------------------------
misc-settings
set max_discovery_time 20
set fuzz_cookies True
set fuzz_form_files True
set fuzz_url_parts True
set fuzz_url_filenames True
back

# -------------------------------------
#               PLUGINS
# -------------------------------------
plugins
crawl web_spider
crawl config web_spider
set only_forward False
set follow_regex http://localhost/public_html/register.php
back

# -------------------------------------
#               AUDIT
# -------------------------------------
audit sqli ssi os_commanding
# -------------------------------------
#               OUTPUT
# -------------------------------------
output console
output config console
set verbose False
back
back

# -------------------------------------
#               TARGET
# -------------------------------------
target
set target http://localhost/public_html/register.php
set target_os unix
set target_framework php
back

cleanup
start
